set(CMAKE_SYSTEM_NAME      Windows CACHE STRING "" FORCE)
set(CMAKE_SYSTEM_PROCESSOR i686    CACHE STRING "")

set(triplet "${CMAKE_SYSTEM_PROCESSOR}-w64-mingw32")
include("${CMAKE_CURRENT_LIST_DIR}/common.cmake")
find_program(CMAKE_RC_COMPILER "${triplet}-windres")

list(APPEND CMAKE_TRY_COMPILE_PLATFORM_VARIABLES
        VCPKG_CRT_LINKAGE
        VCPKG_TARGET_ARCHITECTURE
        VCPKG_C_FLAGS
        VCPKG_CXX_FLAGS
        VCPKG_C_FLAGS_DEBUG
        VCPKG_CXX_FLAGS_DEBUG
        VCPKG_C_FLAGS_RELEASE
        VCPKG_CXX_FLAGS_RELEASE
        VCPKG_LINKER_FLAGS
        VCPKG_LINKER_FLAGS_RELEASE
        VCPKG_LINKER_FLAGS_DEBUG
)

# Because of: https://gcc.gnu.org/bugzilla/show_bug.cgi?id=116159
set(CMAKE_LINKER_TYPE BFD)

string(APPEND CMAKE_C_FLAGS_INIT           " ${VCPKG_C_FLAGS} ")
string(APPEND CMAKE_CXX_FLAGS_INIT         " ${VCPKG_CXX_FLAGS} ")
string(APPEND CMAKE_C_FLAGS_DEBUG_INIT     " ${VCPKG_C_FLAGS_DEBUG} ")
string(APPEND CMAKE_CXX_FLAGS_DEBUG_INIT   " ${VCPKG_CXX_FLAGS_DEBUG} ")
string(APPEND CMAKE_C_FLAGS_RELEASE_INIT   " ${VCPKG_C_FLAGS_RELEASE} ")
string(APPEND CMAKE_CXX_FLAGS_RELEASE_INIT " ${VCPKG_CXX_FLAGS_RELEASE} ")

string(APPEND CMAKE_MODULE_LINKER_FLAGS_INIT " -pthread ${VCPKG_LINKER_FLAGS} ")
string(APPEND CMAKE_SHARED_LINKER_FLAGS_INIT " -pthread ${VCPKG_LINKER_FLAGS} ")
string(APPEND CMAKE_EXE_LINKER_FLAGS_INIT    " -pthread ${VCPKG_LINKER_FLAGS} ")

if(VCPKG_CRT_LINKAGE STREQUAL "static")
    string(APPEND CMAKE_MODULE_LINKER_FLAGS_INIT "-static ")
    string(APPEND CMAKE_SHARED_LINKER_FLAGS_INIT "-static ")
    string(APPEND CMAKE_EXE_LINKER_FLAGS_INIT "-static ")
endif()

string(APPEND CMAKE_MODULE_LINKER_FLAGS_DEBUG_INIT   " ${VCPKG_LINKER_FLAGS_DEBUG} ")
string(APPEND CMAKE_SHARED_LINKER_FLAGS_DEBUG_INIT   " ${VCPKG_LINKER_FLAGS_DEBUG} ")
string(APPEND CMAKE_EXE_LINKER_FLAGS_DEBUG_INIT      " ${VCPKG_LINKER_FLAGS_DEBUG} ")

string(APPEND CMAKE_MODULE_LINKER_FLAGS_RELEASE_INIT " ${VCPKG_LINKER_FLAGS_RELEASE} ")
string(APPEND CMAKE_SHARED_LINKER_FLAGS_RELEASE_INIT " ${VCPKG_LINKER_FLAGS_RELEASE} ")
string(APPEND CMAKE_EXE_LINKER_FLAGS_RELEASE_INIT    " ${VCPKG_LINKER_FLAGS_RELEASE} ")

set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)
